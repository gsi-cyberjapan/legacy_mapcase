<?xml version="1.0" encoding="utf-8"?>
<!--- LayerManager item renderer, design to render a layer in the LayerManager list -->
<s:ItemRenderer xmlns="org.openscales.fx.control.layer.*"
				xmlns:drawing="org.openscales.fx.control.drawing.*"
				xmlns:exdrawing="jp.cyberjapan.webtis.control.drawing.*"
				xmlns:mx="library://ns.adobe.com/flex/mx"
				xmlns:s="library://ns.adobe.com/flex/spark"
				xmlns:fx="http://ns.adobe.com/mxml/2009"
				xmlns:d="http://ns.adobe.com/fxg/2008/dt"
				xmlns:os="org.openscales.fx.control.*"
				width="100%"
				
				xmlns:fxg="assets.fxg.*"
				autoDrawBackground="false"
				implements="org.openscales.fx.control.layer.itemrenderer.ILayerManagerItemRenderer"
				creationComplete="onCreationComplete();" >
	<fx:Metadata>
		<![CDATA[
			
		/** 
		* @eventType org.openscales.core.events.DrawingEvent.CHANGE_ACTIVE_LAYER
		*/ 
		[Event(name="openscales.drawing.change_active_layer", type="org.openscales.core.events.LayerEvent")]
		
		]]>
		
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayCollection;
			import mx.core.UIComponent;
			import mx.events.PropertyChangeEvent;
			
			import org.openscales.core.basetypes.maps.HashMap;
			import org.openscales.core.events.DrawingEvent;
			import org.openscales.core.events.I18NEvent;
			import org.openscales.core.events.LayerEvent;
			import org.openscales.core.events.MapEvent;
			import org.openscales.core.i18n.Catalog;
			import org.openscales.core.layer.Layer;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.layer.ogc.WFS;
			import org.openscales.fx.control.drawing.DrawingToolbar;
			import org.openscales.fx.control.layer.LayerManager;
			
			import spark.components.Label;
			
			public static var DRAWLABEL:String="label";
			public static var DRAWPOINT:String="point";
			public static var DRAWSHAPE:String="shape";
			public static var DRAWPATH:String="path";
			public static var DRAWPOLYGON:String="polygon";
			public static var DRAWSELECT:String="select";
			public static var DRAWDELETE:String="delete";
			public static var DRAWEDIT:String="edit";
			public static var DRAWMOVE:String="move";
			public static var DRAWMANAGE:String="manage";
			public static var DRAWADD:String="add";
			public static var DRAWIMPORT:String="import";
			public static var DRAWEXPORT:String="export";
			public static var EDITSTYLE:String="style";
			public static var DISPINFO:String="info";
			
			[Embed(source="../../../../../../../assets/images/widgetHide.png")]
			protected const BTN_MINIMIZE:Class;
			
			[Embed(source="../../../../../../../assets/images/widgetShow.png")]
			protected const BTN_MAXIMIZE:Class;
			
			private static var _maximizedLayers:HashMap = new HashMap();
			
			private var _timer:Timer;
			
			/**
			 * The current source to the button to display reduce or maximize
			 * @default maximize
			 */
			[Bindable]
			public var _btnReduceMaximize:Class = BTN_MAXIMIZE;
			
			/**
			 * Tootip for reduce maximize button
			 */
			[Bindable]
			public var tooltipReduceMaximize:String = Catalog.getLocalizationForKey('layercontrol.reducemaximize');
			
			/**
			 * To know if the current LayerRenderer display a minimize or maximize item
			 * @default true
			 */
			[Bindable]
			public var isMinimized:Boolean = true;
			
			/**
			 * @private
			 * Define if the current layer is a drawing layer or not
			 * @default false
			 */
			protected var _isDrawingLayer:Boolean = false;
			
			/**
			 * Indicates if the drawingTools has to be displayed
			 */
			[Bindable]
			public var isDisplayedDrawingTools:Boolean = false;
			
			/**
			 * @private
			 * Define if the optionnal group has to be displayed
			 * @default false
			 */
			protected var _optionnalGroupDisplayed:Boolean = false;
			
			/**
			 * @private
			 * Define the difference of size between maximize and minimize renderer
			 * @default 0
			 */
			protected var _maximizeGroupSize:Number = 0;
			
			/**
			 * @private
			 * The position of the layerManagerDrawingTools
			 * @default 0
			 */
			protected var _groupPositionDraw:Number = 0;
			
			/**
			 * @private
			 * The position of the layerManagerOptionnalDisplay
			 * @defualt 0
			 */
			protected var _groupPositionOptionnal:Number = 0;
			
			/**
			 * The layer rendered by this ItemRenderer
			 */
			[Bindable]
			protected var _layer:Layer;
			
			[Bindable]
			protected var _color:uint;
			
			[Bindable]
			protected var _layerManager:LayerManager;
			
			[Bindable]
			protected var _rendererOptions:Object;
			
			[Bindable]
			protected var _drawingToolbar:DrawingToolbar;
			
			/**
			 * The Layer rendered by this itemrenderer
			 */
			public function get layer():Layer{
				
				return _layer;
			}
			
			public function set layer(value:Layer):void{
				
				_layer = value;
			}
			
			/**
			 * The LayerManager this ItemRenderer is displayed in
			 */
			public function get layerManager():LayerManager{
				
				return _layerManager;
			}
			
			public function set layerManager(value:LayerManager):void{
				
				_layerManager = value;
				
				if(_layerManager.map){
					
					_layerManager.map.addEventListener(I18NEvent.LOCALE_CHANGED,this.onMapLanguageChange);
				}
				
				if(_maximizedLayers.getValue(_layerManager) == null){
					
					_maximizedLayers.put(_layerManager, new ArrayCollection());
				}
			}
			
			[Bindable]
			protected var _displayZoomToExtent:Boolean = false;
			
			
			[Bindable]
			protected var _displayMetadatas:Boolean = false;
			
			[Bindable]
			protected var _displayDrawing:Boolean = false;
			
			/**
			 * @inherit
			 * 
			 * The options implemented by the DefaultLayerRenderer are 
			 * 
			 * &lt;LayerManager&gt;
			 *	 	&lt;rendererOptions&gt;
			 *			&lt;fx:Object
			 *				displayLegend="true/false"
			 *				displayZoomToExtent="true/false"
			 *				displayMetadatas="true/false"
			 *              displayDrawing="true/false" /&gt;
			 *		&lt;/rendererOptions&gt;
			 * &lt;/LayerManager&gt;
			 */
			public function get rendererOptions():Object{
				
				return _rendererOptions;
			}
			
			public function set rendererOptions(value:Object):void{
				
				_rendererOptions = value;
				
				if(value){
					if(value['displayZoomToExtent'])
						_displayZoomToExtent = true;
					if(value['displayMetadatas'])
						_displayMetadatas = true;
					if(value['displayDrawing'])
						_displayDrawing = true;
					
					if(_displayZoomToExtent || _displayMetadatas || _displayDrawing)
						_optionnalGroupDisplayed = true;
				}
			}
			
			/**
			 * @inherit
			 */
			public function onMapLanguageChange(event:I18NEvent):void 
			{
				this.tooltipReduceMaximize = Catalog.getLocalizationForKey('layercontrol.reducemaximize');	
			}
			
			/**
			 * According to the group optionnal and drawing tools, set the size and position for this renderer
			 */
			public function setGroupSize():void
			{
				this._maximizeGroupSize = hgroup.height+layerManagerAdditionnalDisplay.height;
				
				// according to the group displayed or not, set the position of the different group
				this._groupPositionOptionnal = hgroup.height + layerManagerAdditionnalDisplay.height;
				this._groupPositionDraw = this._groupPositionOptionnal;
				
				
				if(this._optionnalGroupDisplayed == true)
				{
					this._maximizeGroupSize += layerManagerOptionnalDisplay.height;
					this._groupPositionDraw += layerManagerOptionnalDisplay.height;
				}
				
				if(this._isDrawingLayer && this._displayDrawing)
				{
					this._maximizeGroupSize += layerManagerDrawingTools.height;
				}
				
				layerManagerOptionnalDisplay.y = this._groupPositionOptionnal;
				layerManagerDrawingTools.y = this._groupPositionDraw; // work in progress
			}
			
			protected function onCreationComplete():void{
				
				if (!layerLabel || !_layer || !_layer.map)
					return;
				
				if(this._timer) {
					this._timer.reset();
				} else {
					this._timer = new Timer(500,1);
					this._timer.addEventListener(TimerEvent.TIMER, this.updateItemEnability);
				}
				
				
				this.layerLabel.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE, this.propertyChange);
				this.layerLabel.addEventListener(MouseEvent.CLICK, this.onThisLabelClick);
				this._layer.map.addEventListener(DrawingEvent.CHANGE_ACTIVE_LAYER, this.onOtherLabelClick);
				this._layer.map.addEventListener(LayerEvent.LAYER_REMOVED, this.onLayerRemoved);
				this._layer.map.addEventListener(MapEvent.RELOAD, this.resetTimer);
			}
			
			private function getComponentFromString(value:String):UIComponent{
				var myComponent:UIComponent = null;
				switch(value) {
					case DRAWLABEL:
						myComponent = this.drawLabel;break;
					case DRAWPOINT:
						myComponent = this.drawPoint;break;
					case DRAWSHAPE:
						myComponent = this.drawShape;break;
					case DRAWPATH:
						myComponent = this.drawPath;break;
					case DRAWPOLYGON:
						myComponent = this.drawPolygon;break;
					case DRAWSELECT:
						myComponent = this.exSelectFeatures;break;
					case DRAWDELETE:
						myComponent = this.deleteFeatures;break;
					case DRAWEDIT:
						myComponent = this.exEditFeatures;break;
					case DRAWMOVE:
						myComponent = this.moveFeatures;break;
//					case DRAWMANAGE:
//						myComponent = this.manageAttributes;break;
					case DRAWEXPORT:
						myComponent = this.exExportFeature;break;
					case EDITSTYLE:
						myComponent = this.exEditStyle;break;
					case DISPINFO:
						myComponent = this.dispInformation;break;
				}
				
				return myComponent;
			}
			
			private function resetTimer(event:MapEvent):void{
				this._timer.reset();
				this._timer.start();
			} 
			
			protected function updateItemEnability(event:Event=null):void{
				if(this._layer && this.layer.map){
					this.enabled = this._layer.available;

				}
			}
			
			/**
			 * Called when a layer is removed
			 */
			protected function onLayerRemoved(event:LayerEvent):void{
				if(event.layer.selected && this._layer && this._layer.map){
					event.layer.selected = false;
					var evt:DrawingEvent = new DrawingEvent(DrawingEvent.CHANGE_ACTIVE_LAYER);
					evt.layer = null;
					this._layer.map.dispatchEvent(evt);
				}
			}
			
			/**
			 * Update the layer on its label click
			 */
			private function onThisLabelClick(evt:MouseEvent):void{
				
				var displayDrawingToolbar:Boolean = true;
				for(var k:uint = 0; k < this.layer.map.controls.length; k++){
					if(this.layer.map.controls[k] is DrawingToolbar)
						if((this.layer.map.controls[k] as DrawingToolbar).mode != "multiple") {
							displayDrawingToolbar = false;
						} else {
							this._drawingToolbar = (this.layer.map.controls[k] as DrawingToolbar);
						}
				
				}
				if(this._layer.hasOwnProperty("features") && !displayDrawingToolbar){
					var event:DrawingEvent = new DrawingEvent(DrawingEvent.CHANGE_ACTIVE_LAYER);
					event.layer = this._layer as VectorLayer;
					this._layer.selected = true;
					_color = this._layer.selected ? 0xd0d0d0 : 0xffffff;
					this._layer.map.dispatchEvent(event);
				}
			}
			
			/**
			 * Update the layer on other label click
			 */
			private function onOtherLabelClick(evt:DrawingEvent):void{
				
				if(!evt.layer)
					return;
				if(!(evt.layer.hasOwnProperty("features")) || evt.layer != this._layer && this._layer){
					this._layer.selected = false;
					_color = this._layer.selected ? 0xd0d0d0 : 0xffffff;
				}
			}
			
			/**
			 * @inherit
			 */
			override public function set data(value:Object):void 
			{
				super.data = value;
				this._layer = value as Layer;
				
				if((_maximizedLayers.getValue(_layerManager) as ArrayCollection).getItemIndex(_layer) < 0){
					
					this.isMinimized = true;
				}
				else{
					
					this.isMinimized = false;
				}
				this.manageToogle();
				this._isDrawingLayer = false;
				if((this._layer != null) )
				{	
					layerLabel.text = this._layer.displayedName;
					
					if((this._layer is VectorLayer) && (this._layer as VectorLayer).editable)
						this._isDrawingLayer = true;
					else
						this._isDrawingLayer = false;
					
					var displayDrawingToolbar:Boolean = true;
					for(var k:uint = 0; k < this.layer.map.controls.length; k++){
						if(this.layer.map.controls[k] is DrawingToolbar)
							if((this.layer.map.controls[k] as DrawingToolbar).mode != "multiple") {
								displayDrawingToolbar = false;
							} else {
								this._drawingToolbar = (this.layer.map.controls[k] as DrawingToolbar);
								
								//Need to show/hide components from toolbar
								for(var i:uint = 0 ; i < this._drawingToolbar.multipleModeInitTools.length ; i++) {
									if(this._drawingToolbar.multipleModeDisplayTools.indexOf(this._drawingToolbar.multipleModeInitTools[i]) < 0) {
										this.getComponentFromString(this._drawingToolbar.multipleModeInitTools[i]).visible = false;
										this.getComponentFromString(this._drawingToolbar.multipleModeInitTools[i]).width = 0;
										this.getComponentFromString(this._drawingToolbar.multipleModeInitTools[i]).height = 0;
									}
								}
								
							}
					}
					_color = (this._layer.selected && !displayDrawingToolbar) ? 0xd0d0d0 : 0xffffff;
					this.isDisplayedDrawingTools = !this.isMinimized && this._isDrawingLayer && displayDrawingToolbar;
					this.updateItemEnability(null);
				}
				
				this.setGroupSize();
				
			}
			
			private function manageToogle():void {
				if(!this.layer || !this.layer.map) {
					this.isDisplayedDrawingTools = false;
					this._btnReduceMaximize = BTN_MAXIMIZE;
					return;
				}
				var displayDrawingToolbar:Boolean = true;
				for(var k:uint = 0; k < this.layer.map.controls.length; k++){
					if(this.layer.map.controls[k] is DrawingToolbar) {
						if((this.layer.map.controls[k] as DrawingToolbar).mode != "multiple") {
							displayDrawingToolbar = false;
							break;
						} else {
							this._drawingToolbar = (this.layer.map.controls[k] as DrawingToolbar);
						}
					}
				}
				this.isDisplayedDrawingTools = !this.isMinimized && this._isDrawingLayer && displayDrawingToolbar;
				
				this._btnReduceMaximize = this.isMinimized ?  BTN_MAXIMIZE : BTN_MINIMIZE;
				
				var index:int;
				if(isMinimized){
					index = (_maximizedLayers.getValue(_layerManager) as ArrayCollection).getItemIndex(_layer);
					if(index!=-1)
						(_maximizedLayers.getValue(_layerManager) as ArrayCollection).removeItemAt(index);					
				}
				else{
					index = (_maximizedLayers.getValue(_layerManager) as ArrayCollection).getItemIndex(_layer);
					if(index==-1)
						(_maximizedLayers.getValue(_layerManager) as ArrayCollection).addItem(_layer);
				}
			}
			
			/**
			 * Reduce or maximize the current LayerManager
			 */
			public function reduceMaximizeLayerManager(event:Event):void 
			{
				this.isMinimized = !this.isMinimized;
				this.manageToogle();
			}
			
			
			
			private function propertyChange(event:PropertyChangeEvent):void
			{
				this.invalidateDisplayList();
			}    
			
		]]>
	</fx:Script>
	
	<s:layout>
		<s:VerticalLayout />
	</s:layout>
	
	<s:SkinnableContainer width="100%"
						 
						  id="hgroup" backgroundColor="{_color}" >
		<s:layout>
			<s:HorizontalLayout 
				paddingLeft="2"
				paddingBottom="2"
				paddingRight="4"
				paddingTop="2"
				verticalAlign="middle" />
		</s:layout>
		
		<ChangeLayerVisibility layer="{_layer}" />
		
		<s:Label id="layerLabel" width="100%" text="{_layer.displayedName}" />
		
		<ChangeLayerPosition id="changeLayerPositionControl" layer="{_layer}" layerManager="{_layerManager}" />
		
		<os:IconButton id="layerManagerReduceMaximize"
					   width="16"
					   height="16"
					   click="reduceMaximizeLayerManager(event)"
					   icon="{this._btnReduceMaximize}"
					   iconOver="{this._btnReduceMaximize}"
					   iconDown="{this._btnReduceMaximize}"
					   toolTip="{this.tooltipReduceMaximize}" />
		<DeleteLayer id="layerManagerDeleteLayer" layer="{_layer}" />
	</s:SkinnableContainer>
	
	
	<s:SkinnableContainer id="layerManagerAdditionnalDisplay"  visible="{!this.isMinimized}" includeInLayout="{!this.isMinimized}" >  
		<ChangeLayerAlpha id="layerControlOpacity" layer="{_layer}" />
	</s:SkinnableContainer>
	
	<s:SkinnableContainer width="100%" id="layerManagerDrawingTools" visible="{this.isDisplayedDrawingTools}" includeInLayout="{this.isDisplayedDrawingTools}" >
		
		<s:layout>
			<s:VerticalLayout />
		</s:layout>
		
		
		<s:SkinnableContainer  width="100%" visible="{this.isDisplayedDrawingTools}" includeInLayout="{this.isDisplayedDrawingTools}">		
			<s:layout>
				<s:HorizontalLayout gap="0"/>
			</s:layout>
			<drawing:DrawLabel id="drawLabel" map="{this._layer.map}"  target="{this._layer as VectorLayer}"  />
			<exdrawing:DrawMarker id="drawPoint" map="{this._layer.map}" style="{this._drawingToolbar.pointStyle}" target="{this._layer as VectorLayer}"  />
			<drawing:DrawPath id="drawPath" map="{this._layer.map}" style="{this._drawingToolbar.pathStyle}" target="{this._layer as VectorLayer}"  />
			<drawing:DrawPolygon id="drawPolygon" map="{this._layer.map}" style="{this._drawingToolbar.polygonStyle}" target="{this._layer as VectorLayer}"  />
			<drawing:DrawShape id="drawShape" map="{this._layer.map}" style="{this._drawingToolbar.shapeStyle}" target="{this._layer as VectorLayer}"  />
			<exdrawing:DrawCircle id="drawCircle" map="{this._layer.map}" style="{this._drawingToolbar.polygonStyle}" target="{this._layer as VectorLayer}"  />
		</s:SkinnableContainer>
		
		<s:SkinnableContainer  width="100%" visible="{this.isDisplayedDrawingTools}" includeInLayout="{this.isDisplayedDrawingTools}">
			
			<s:layout>
				<s:HorizontalLayout gap="0"/>
			</s:layout>
			<exdrawing:ExSelectFeatures id="exSelectFeatures" fxMap="{this._drawingToolbar.fxMap}" map="{this._layer.map}" target="{this._layer as VectorLayer}"/>
			<drawing:DeleteFeatures id="deleteFeatures" map="{this._layer.map}" target="{this._layer as VectorLayer}" />
			<exdrawing:ExEditFeatures id="exEditFeatures" map="{this._layer.map}" target="{this._layer as VectorLayer}"  />
			<exdrawing:ExMoveFeatures id="moveFeatures" map="{this._layer.map}" target="{this._layer as VectorLayer}"  />
			<!--<drawing:ManageAttributes id="manageAttributes" map="{this._layer.map}" target="{this._layer as VectorLayer}"  />-->
			<exdrawing:ExEditStyle id="exEditStyle" map="{this._layer.map}" target="{this._layer as VectorLayer}" />
			<exdrawing:ExExportFeature id="exExportFeature" map="{this._layer.map}" layer="{this._layer as VectorLayer}" mode="only" />
			<exdrawing:DispInformation id="dispInformation" map="{this._layer.map}" target="{this._layer as VectorLayer}" />
			
		</s:SkinnableContainer>
		
	</s:SkinnableContainer>
	
	<s:SkinnableContainer  width="100%" id="layerManagerOptionnalDisplay" visible="{!this.isMinimized &amp;&amp; (_displayZoomToExtent || _displayMetadatas) }" includeInLayout="{!this.isMinimized &amp;&amp; (_displayZoomToExtent || _displayMetadatas)}" >
		<LayerZoomToExtent id="layerControlZoomToExtent" visible="{_displayZoomToExtent}" layer="{_layer}" />
		<LayerMetadata x="40" id="layerControlMetadatas" visible="{_displayMetadatas}" />
	</s:SkinnableContainer>
	
	
	
</s:ItemRenderer>
