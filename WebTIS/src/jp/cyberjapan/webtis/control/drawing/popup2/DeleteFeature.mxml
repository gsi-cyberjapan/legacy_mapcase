<?xml version="1.0" encoding="utf-8"?>
<os:IconButton xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:os="org.openscales.fx.control.*"
			   implements="org.openscales.core.control.IControl"
			   toolTip="{this.tooltip}"
			   icon="{_btnDelete}"
			   alpha="{_alpha}"
			   width="25"
			   height="25"
			   click="onClick()"
			   enabled="{active}">
	
	<fx:Script>
		<![CDATA[
			import org.openscales.core.Map;
			import org.openscales.core.control.IControl;
			import org.openscales.core.events.DrawingEvent;
			import org.openscales.core.events.FeatureEvent;
			import org.openscales.core.events.I18NEvent;
			import org.openscales.core.feature.Feature;
			import org.openscales.core.feature.LabelFeature;
			import org.openscales.core.feature.LineStringFeature;
			import org.openscales.core.feature.MultiLineStringFeature;
			import org.openscales.core.feature.MultiPointFeature;
			import org.openscales.core.feature.PointFeature;
			import org.openscales.core.handler.IHandler;
			import org.openscales.core.handler.feature.DragFeatureHandler;
			import org.openscales.core.handler.feature.SelectFeaturesHandler;
			import org.openscales.core.i18n.Catalog;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.style.Style;
			import org.openscales.core.style.font.Font;
			import org.openscales.core.style.symbolizer.LineSymbolizer;
			import org.openscales.core.style.symbolizer.PolygonSymbolizer;
			import org.openscales.core.style.symbolizer.TextSymbolizer;
			import org.openscales.fx.control.layer.LayerManager;
			import org.openscales.geometry.Point;
			import org.openscales.geometry.basetypes.Location;
			import org.openscales.geometry.basetypes.Pixel;
			
		// hamas : 13.09.10 追加
			private var _targetFeature:Feature = null;
			public function set targetFeature( feature:Feature ) : void {
				this._targetFeature =  feature;
				this._target = feature.layer;
			}
			
			[Bindable]
			private var _alpha:Number = 0.3;
			
			[Bindable]
			private var tooltip:String = Catalog.getLocalizationForKey('drawing.delete');
			
			[Bindable]
			[Embed(source="../../../../../../../assets/images/Icons-21.png")]
			private var _btnDelete:Class;
			
			private var _target:VectorLayer;
			
			[Bindable]
			private var _map:Map;
			private var _active:Boolean = true;
			
			[Bindable]
			private var _handler:SelectFeaturesHandler = new SelectFeaturesHandler();
			private var _isClick:Boolean = false;
			private var _drawingMode:String;
			
			/**
			 * Array of some features that may not be deleted.
			 */
			private var _undeletableFeatures:Vector.<Feature> = new Vector.<Feature>();
			
			/**
			 * Indicates for Drawing mode
			 * multiple : tools are in the LayerSwitcher
			 * unique : tools are in the DrawingToolbar
			 */
			[Bindable]
			public function get drawingMode():String{
				
				return _drawingMode;
			}
			
			public function set drawingMode(value:String):void{
				_drawingMode = value;
			}
			
			/**
			 * The drag feature Handler
			 */
			public function get handler():SelectFeaturesHandler
			{
				return this._handler;
			}
			
			/**
			 * Add an unselectable feature.
			 */
			public function addUndeletableFeature(feature:Feature):void
			{
				if(feature!=null){
					if (this._handler)
					{
						_handler.addUnselectableFeature(feature);
					}
					if(_undeletableFeatures.indexOf(feature)==-1)
					{
						_undeletableFeatures.push(feature);
					}
				}
			}
			
			/**
			 * Flag for activating and deactivating the component
			 * @default true;
			 */
			[Bindable]
			public function get active():Boolean{
				
				return _active;
			}
			public function set active(value:Boolean):void{
				
				_active = value;
				if(! _active){
					_handler.active = false;
				}
			}
			
			/**
			 * Map controled by this control
			 */
			public function get map():Map{
				
				return _map;
			}
			public function set map(value:Map):void{
				
				this._map = value;
				this._map.addEventListener(DrawingEvent.DRAW_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.EDIT_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.MOVE_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.DELETE_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.SELECT_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.CHANGE_ACTIVE_LAYER, onChangeActiveLayer);
				this._map.addEventListener(I18NEvent.LOCALE_CHANGED, onMapLanguageChange);
				this._map.addEventListener(FeatureEvent.FEATURE_SELECT, onSelectedFeature);
			// hamas : 13.09.10 追加
				this._map.addEventListener("POP_CLOSE_DELETE", onPopupCloseHandler);
			}
			
			/**
			 * Position (x,y) of the handler
			 */
			public function get position():Pixel{
				
				return new Pixel(x,y);
			}
			public function set position(value:Pixel):void{
				
				if(value){
					x = value.x;
					y = value.y;
				}
			}
			
			/**
			 * Layer the features will be moved
			 */
			public function get target():VectorLayer{
				
				return _target;
			}
			
			public function set target(value:VectorLayer):void{
				
				_target = value;
			}
			
			/**
			 * Update the feature style when Mouse goes over the feature
			 * event occured
			 */
			private function onFeatureOver(evt:FeatureEvent):void
			{
				if(evt.feature.layer == this.target)
				{
					evt.feature.originalStyle = evt.feature.style;
					
					if (evt.feature is LabelFeature) {
						evt.feature.style = Style.getNegativeLabelStyle(evt.feature.style);
					} else if(evt.feature is PointFeature || evt.feature is MultiPointFeature) {
						//evt.feature.style = Style.getDefaultSelectedPointStyle();
					} else if (evt.feature is LineStringFeature || evt.feature is MultiLineStringFeature) {
						evt.feature.style = Style.getDefaultSelectedLineStyle();
						(evt.feature.style.rules[0].symbolizers[0] as LineSymbolizer).stroke.width = (evt.feature.originalStyle.rules[0].symbolizers[0] as LineSymbolizer).stroke.width;
					} else {
						evt.feature.style = Style.getDefaultSelectedPolygonStyle();
						(evt.feature.style.rules[0].symbolizers[0] as PolygonSymbolizer).stroke.width = (evt.feature.originalStyle.rules[0].symbolizers[0] as PolygonSymbolizer).stroke.width;
					}
					
					evt.feature.draw();
				}
			}
			
			/**
			 * Update the feature style when mouse goes out the feature
			 * event occured
			 */
			private function onFeatureOut(evt:FeatureEvent):void
			{
				if(evt.feature.layer == this.target)
				{	
					if(evt.feature.originalStyle != null) {
						evt.feature.style = evt.feature.originalStyle;
					} else {
						if (evt.feature is LabelFeature) {
							evt.feature.style = Style.getNegativeLabelStyle(evt.feature.style);
						} else if(evt.feature is PointFeature || evt.feature is MultiPointFeature) {
							//evt.feature.style = Style.getDefaultPointStyle();
						} else if (evt.feature is LineStringFeature || evt.feature is MultiLineStringFeature) {
							evt.feature.style = Style.getDefaultLineStyle();
						} else {
							evt.feature.style = Style.getDefaultPolygonStyle();
						}
					}
					
					evt.feature.draw();
				}
			}
			
			/**
			 * To activate or disactivate the handler
			 */
			private function activate():void{
				
				_handler.active = true;
				_handler.map.mouseNavigationEnabled = true;
				_handler.map.panNavigationEnabled = true;
				_handler.map.zoomNavigationEnabled = true;
				_handler.map.keyboardNavigationEnabled = true;
				_alpha = 1;
				
				if(this._map) {
					this._map.addEventListener(FeatureEvent.FEATURE_OVER, onFeatureOver);
					this._map.addEventListener(FeatureEvent.FEATURE_OUT, onFeatureOut);
				}
			}
			private function disactivate():void{
				
				if (_handler)
				{
					_handler.active = false;
					_handler = null;
				}
				_alpha = 0.3;
				
				if(this.map) {
					var evt:DrawingEvent = new DrawingEvent(DrawingEvent.DELETE_HANDLER_DESACTIVATED);
					this._map.dispatchEvent(evt);
					
					this._map.removeEventListener(FeatureEvent.FEATURE_OVER, onFeatureOver);
					this._map.removeEventListener(FeatureEvent.FEATURE_OUT, onFeatureOut);
				}
			}
			
			/**
			 * This function is called when a feature is selected
			 */
			private function onSelectedFeature(event:FeatureEvent):void
			{
				if(!this.target)
					return;
				if(event.feature.layer != this.target)
					return;
				if(this._handler && this._handler.active){
					this.target.removeFeature(event.feature, true);
				}
			// hamas : 13.09.10 追加
				this._map.dispatchEvent(new Event("POP_CLOSE_DELETE"));
			}
			
			/**
			 * On button click, dispatch a DrawingEvent.EDIT_HANDLER_ACTIVATED event
			 */
			public function onClick():void{
				if(!this.target)
					return;
				
				// Disactive this component
				var evt:DrawingEvent;
				if(this._handler){
					disactivate();
					return;
				} else {
					// Add a new SelectFeaturesHandler
					this._handler = new SelectFeaturesHandler(this._map,true,true,false,false);
					this._handler.clickOut = false;
					this._handler.toggle = false;
					this._handler.redrawFeatureOnSelection = false;
					this._handler.layers[0] = _target;
					this._handler.unselectableFeatures = this._undeletableFeatures;
				}
				
				// Disactive others drawing tools components
				this._isClick = true;
				evt = new DrawingEvent(DrawingEvent.DELETE_HANDLER_ACTIVATED);
				evt.activeHandler = this.id;
				evt.layer = this.target;
				this._map.dispatchEvent(evt);
				
				// Disactive mesure tools components
				evt = new DrawingEvent(DrawingEvent.CHANGE_ACTIVE_HANDLER);
				evt.activeHandler = null;
				this._map.dispatchEvent(evt);
				
				// Active this component
				activate();
				
			// hamas : 13.09.10 追加
				this._map.dispatchEvent( new FeatureEvent(FeatureEvent.FEATURE_SELECT, this._targetFeature));
			}
			
			/**
			 * Check the handler when a DrawingEvent.DRAW_HANDLER_ACTIVATED or
			 * DrawingEvent.EDIT_HANDLER_ACTIVATED or DrawingEvent.MOVE_HANDLER_ACTIVATED
			 * or DrawingEvent.SELECT_HANDLER_ACTIVATED event occured
			 */
			private function onChangeActiveHandler(evt:DrawingEvent):void{
				
				if(this._handler && !this._isClick)
					disactivate();
				else
					this._isClick = false;
			}
			
			/**
			 * Update the target on active layer changed
			 */
			private function onChangeActiveLayer(evt:DrawingEvent):void{
				
				if(this.drawingMode == 'unique')
				{
					if(this._handler){
						disactivate();
					}
					this.target = evt.layer;
					
					if(!evt.layer || !evt.layer.editable) {
						this.buttonMode = false;
						this.useHandCursor = false;
						this.toolTip = Catalog.getLocalizationForKey('drawing.selectDrawingLayer');
					} else {
						this.buttonMode = true;
						this.useHandCursor = true;
						this.toolTip = Catalog.getLocalizationForKey('drawing.delete');
					}
				}
			}
			
			/**
			 * Update texts on map language changed
			 */
			public function onMapLanguageChange(event:I18NEvent):void{
				
				this.tooltip = Catalog.getLocalizationForKey('drawing.delete');
			}
			
			// Unused functions
			public function draw():void{
				
				this.invalidateDisplayList();
			}
			public function destroy():void{}
			
		// hamas : 13.09.10 追加
			private function onPopupCloseHandler( evt:Event ):void {
				// Disactive this component
				this.disactivate();
				return;
			}			
		]]>
	</fx:Script>
</os:IconButton>
