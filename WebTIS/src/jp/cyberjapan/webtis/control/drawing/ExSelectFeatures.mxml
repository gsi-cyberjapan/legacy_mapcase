<?xml version="1.0" encoding="utf-8"?>
<os:IconButton xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:os="org.openscales.fx.control.*"
			   implements="org.openscales.core.control.IControl"
			   toolTip="{this.tooltip}"
			   icon="{_btnSelect}"
			   alpha="{_alpha}"
			   width="25"
			   height="25"
			   click="onClick()"
			   enabled="{active}">
	<fx:Script>
		<![CDATA[
			import jp.cyberjapan.webtis.control.drawing.popup.ExEditFeaturesPopupRenderer;
			import mx.controls.Alert;
			import org.openscales.core.control.IControl;
			import org.openscales.core.events.DrawingEvent;
			import org.openscales.core.events.FeatureEvent;
			import org.openscales.core.events.I18NEvent;
			import org.openscales.core.feature.Feature;
			import org.openscales.core.feature.LabelFeature;
			import org.openscales.core.feature.LineStringFeature;
			import org.openscales.core.feature.MultiLineStringFeature;
			import org.openscales.core.feature.MultiPointFeature;
			import org.openscales.core.feature.PointFeature;
			import org.openscales.core.handler.feature.draw.EditFeaturesHandler;
			import org.openscales.core.handler.feature.SelectFeaturesHandler;
			import org.openscales.core.handler.IHandler;
			import org.openscales.core.i18n.Catalog;
			import org.openscales.core.layer.VectorLayer;
			import org.openscales.core.Map;
			import org.openscales.core.style.font.Font;
			import org.openscales.core.style.Style;
			import org.openscales.core.style.symbolizer.LineSymbolizer;
			import org.openscales.core.style.symbolizer.PolygonSymbolizer;
			import org.openscales.core.style.symbolizer.Symbolizer;
			import org.openscales.core.style.symbolizer.TextSymbolizer;
			import org.openscales.fx.control.drawing.popup.EditFeaturesPopupRenderer;
			import org.openscales.fx.control.layer.LayerManager;
			import org.openscales.fx.FxMap;
			import org.openscales.fx.popup.FxPopup;
			import org.openscales.fx.popup.renderer.FxPopupRenderer;
			import org.openscales.geometry.basetypes.Location;
			import org.openscales.geometry.basetypes.Pixel;
			import org.openscales.geometry.Point;
			
			[Bindable]
			private var _alpha:Number = 0.3;
			
			[Bindable]
			private var tooltip:String = Catalog.getLocalizationForKey('drawing.select');
			
			[Bindable]
			[Embed(source="../../../../../../assets/images/Icons-06.png")]
			private var _btnSelect:Class;
			
			private var _active:Boolean = true;
			private var _clickout:Boolean = true;
			private var _hover:Boolean = false;
			private var _toggle:Boolean = false;
			private var _map:Map;
			private var _fxMap:FxMap;
			private var _target:VectorLayer;
			protected var _handler:EditFeaturesHandler = null;
			protected var _previousSelectFeaturesHandler:SelectFeaturesHandler = null;
			private var _isClick:Boolean = false;
			private var _fxpopup:FxPopup;
			private var _drawingMode:String;
			
			private var fxpopupRenderer:FxPopupRenderer = new ExEditFeaturesPopupRenderer();
			
			/**
			 * Indicates for Drawing mode
			 * multiple : tools are in the LayerSwitcher
			 * unique : tools are in the DrawingToolbar
			 */
			[Bindable]
			public function get drawingMode():String{
				
				return _drawingMode;
			}
			
			public function set drawingMode(value:String):void{
				_drawingMode = value;
			}
			
			/**
			 * Flag for activating and deactivating the component
			 * @default true;
			 */
			[Bindable]
			public function get active():Boolean{
				
				return _active;
			}
			public function set active(value:Boolean):void{
				
				_active = value;
				if(this._handler && !_active){
					_handler.active = false;
				}
			}
			
			/**
			 * If true, a feature is unselected by clicking out of it
			 * @default true;
			 */
			public function get clickout():Boolean{
				
				return this._clickout;
			}
			public function set clickout(value:Boolean):void{
				
				this._clickout = value;
			}
			
			/**
			 * If true, a feature is selected on mouseover
			 * @default false;
			 */
			public function get hover():Boolean{
				
				return this._hover;
			}
			public function set hover(value:Boolean):void{
				
				this._hover = value;
			}
			
			/**
			 * If true, a selected feature is unselected by clicking on it
			 * @default false;
			 */
			public function get toggle():Boolean{
				
				return this._toggle;
			}
			public function set toggle(value:Boolean):void{
				
				this._toggle = value;
			}
			
			public function get fxpopup():FxPopup{
				
				return this._fxpopup;
			}
			public function set fxpopup(value:FxPopup):void{
				
				this._fxpopup = value;
			}
			
			/**
			 * Map controled by this control
			 */
			public function get map():Map{
				
				return this._map;
			}
			
			public function set map(value:Map):void{
				this._map = value;
				this._map.addEventListener(DrawingEvent.DRAW_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.EDIT_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.MOVE_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.DELETE_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.SELECT_HANDLER_ACTIVATED, onChangeActiveHandler);
				this._map.addEventListener(DrawingEvent.CHANGE_ACTIVE_LAYER, onChangeActiveLayer);
				this._map.addEventListener(I18NEvent.LOCALE_CHANGED, onMapLanguageChange);
				
			}
			
			/**
			 * FxMap controled by this control
			 */
			public function get fxMap():FxMap{
				
				return this._fxMap;
			}
			
			public function set fxMap(value:FxMap):void{
				this._fxMap = value;
			}
			
			/**
			 * Position (x,y) of the handler
			 */
			public function get position():Pixel{
				
				return new Pixel(x,y);
			}
			public function set position(value:Pixel):void{
				
				if(value){
					x = value.x;
					y = value.y;
				}
			}
			
			/**
			 * Layer the features will be selected
			 */
			public function get target():VectorLayer{
				
				return this._target;
			}
			public function set target(value:VectorLayer):void{
				
				this._target = value;
			}
			
			/**
			 * Update the features to edit when a FeatureEvent.FEATURE_SELECTED 
			 * event occured
			 */
			public function onFeatureSelected(evt:FeatureEvent):void
			{
				if(evt.feature.layer == _target)
				{
					if(_fxpopup){
						_fxpopup.destroy();
					}
					_fxpopup = null;
					_fxpopup = new FxPopup();
					_fxpopup.fxmap = fxMap;
					
					// カスタマイズポップアップ
					//var fxpopupRenderer:EditFeaturesPopupRenderer = new ExEditFeaturesPopupRenderer();
					var fxpopupRenderer:FxPopupRenderer = new ExEditFeaturesPopupRenderer();
					_fxpopup.popupRenderer = fxpopupRenderer;
					
					_fxpopup.feature = evt.feature;
					var px:Pixel = _fxpopup.fxmap.map.getMapPxFromLocation(evt.feature.lonlat);
					_fxpopup.relativePosition = _fxpopup.calculateRelativePosition(px);
					_fxpopup.position = px;
					_fxpopup.setCurrentState(fxpopup.relativePosition,false);
					fxpopupRenderer.titleContent = Catalog.getLocalizationForKey("editfeatureattributes.title");
					_fxpopup.HEIGHT = 350;
					_fxMap.addFxPopup(fxpopup,true);
					_fxpopup.visible = true;
				}
			}
			
			/**
			 * Update the feature style when Mouse goes over the feature
			 * event occured
			 */
			private function onFeatureOver(evt:FeatureEvent):void
			{
				if(evt.feature.layer == this.target)
				{
					evt.feature.originalStyle = evt.feature.style;
					
					if (evt.feature is LabelFeature) {
						evt.feature.style = Style.getNegativeLabelStyle(evt.feature.style);
					} else if(evt.feature is PointFeature || evt.feature is MultiPointFeature) {
						//evt.feature.style = Style.getDefaultSelectedPointStyle();
					} else if (evt.feature is LineStringFeature || evt.feature is MultiLineStringFeature) {
						evt.feature.style = Style.getDefaultSelectedLineStyle();
						(evt.feature.style.rules[0].symbolizers[0] as LineSymbolizer).stroke.width = (evt.feature.originalStyle.rules[0].symbolizers[0] as LineSymbolizer).stroke.width;
					} else {
						evt.feature.style = Style.getDefaultSelectedPolygonStyle();
						(evt.feature.style.rules[0].symbolizers[0] as PolygonSymbolizer).stroke.width = (evt.feature.originalStyle.rules[0].symbolizers[0] as PolygonSymbolizer).stroke.width;
					}
					
					evt.feature.draw();
				}
			}
			
			/**
			 * Update the feature style when mouse goes out the feature
			 * event occured
			 */
			private function onFeatureOut(evt:FeatureEvent):void
			{
				if(evt.feature.layer == this.target)
				{	
					if(evt.feature.originalStyle != null) {
						evt.feature.style = evt.feature.originalStyle;
					} else {
						if (evt.feature is LabelFeature) {
							evt.feature.style = Style.getNegativeLabelStyle(evt.feature.style);
						} else if(evt.feature is PointFeature || evt.feature is MultiPointFeature) {
							//evt.feature.style = Style.getDefaultPointStyle();
						} else if (evt.feature is LineStringFeature || evt.feature is MultiLineStringFeature) {
							evt.feature.style = Style.getDefaultLineStyle();
						} else {
							evt.feature.style = Style.getDefaultPolygonStyle();
						}
					}
					
					evt.feature.draw();
				}
			}
			
			/**
			 * To activate the handler
			 */
			private function activate():void
			{
				//Alert.show("activate");
				if(this.map) {
					this._map.addEventListener(FeatureEvent.FEATURE_SELECTED, onFeatureSelected);
					this._map.addEventListener(FeatureEvent.FEATURE_OVER, onFeatureOver);
					this._map.addEventListener(FeatureEvent.FEATURE_OUT, onFeatureOut);
				}
				
				for each(var ih:IHandler in this._map.controls)
				{
					if(ih is SelectFeaturesHandler && ih != this._handler)
					{
						// Store the existing SelectFeaturesHandler
						this._previousSelectFeaturesHandler = (ih as SelectFeaturesHandler);
						(ih as SelectFeaturesHandler).map = null;
						this.map.removeControl(ih);
					}
				}
				// Add a new SelectFeaturesHandler
				if(this._hover)
					this._handler = new EditFeaturesHandler(this._map,true,true,false,true);
				else
					this._handler = new EditFeaturesHandler(this._map,true,true,false,false);
				this._handler.layers[0] = this._target;
				this._handler.clickOut = this._clickout;
				this._handler.toggle = this._toggle;
				
				_alpha = 1;
			}
			
			/**
			 * To disactivate the handler
			 */
			private function disactivate():void{
				
				var evt:DrawingEvent = new DrawingEvent(DrawingEvent.SELECT_HANDLER_DESACTIVATED);
				evt.layer = this.target;
				this.map.dispatchEvent(evt);
				
				// Manage the new SelectFeaturesHandler
				this._handler.clearSelection();
				this._handler.active = false;
				this._handler.map = null;
				this.map.removeControl(this._handler);
				
				// Add the previous SelectFeaturesHandler
				if(this._previousSelectFeaturesHandler)
					this._previousSelectFeaturesHandler.map = this.map;
				
				_alpha = 0.3;
				
				if(this.map) {
					this._map.removeEventListener(FeatureEvent.FEATURE_SELECTED, onFeatureSelected);
					this._map.removeEventListener(FeatureEvent.FEATURE_OVER, onFeatureOver);
					this._map.removeEventListener(FeatureEvent.FEATURE_OUT, onFeatureOut);
				}
				
				if(fxpopup){
					fxpopup.destroy();
				}
			}
			
			/**
			 * On button click, dispatch a DrawingEvent.SELECT_HANDLER_ACTIVATED event
			 */
			public function onClick():void{
				
				if(!this.target)
					return;
				
				// Disactive this component
				var evt:DrawingEvent;
				if(this._handler && this._handler.active){
					disactivate();
					return;
				}
				
				// Disactive others drawing tools components
				this._isClick = true;
				evt = new DrawingEvent(DrawingEvent.SELECT_HANDLER_ACTIVATED);
				evt.activeHandler = this.id;
				evt.layer = this.target;
				this.map.dispatchEvent(evt);
				
				// Disactive mesure tools components
				evt = new DrawingEvent(DrawingEvent.CHANGE_ACTIVE_HANDLER);
				evt.activeHandler = this.id;
				evt.layer = this.target;
				this.map.dispatchEvent(evt);
				
				// Active this component
				activate();
			}
			
			/**
			 * Check the handler when a DrawingEvent.DRAW_HANDLER_ACTIVATED or
			 * DrawingEvent.EDIT_HANDLER_ACTIVATED or DrawingEvent.MOVE_HANDLER_ACTIVATED
			 * or DrawingEvent.SELECT_HANDLER_ACTIVATED event occured
			 */
			private function onChangeActiveHandler(evt:DrawingEvent):void{
				
				if(this._handler && this._handler.active && !this._isClick) {
					disactivate();
					this._map.keyboardNavigationEnabled = true;
				}
				else
					this._isClick = false;
			}
			
			/**
			 * Update the target on active layer changed
			 */
			private function onChangeActiveLayer(evt:DrawingEvent):void{
				
				if(this.drawingMode == 'unique')
				{
					if(this._handler && this._handler.active){
						disactivate();
						this._map.mouseNavigationEnabled = true;
						this._map.panNavigationEnabled = true;
						this._map.zoomNavigationEnabled = true;
						this._map.keyboardNavigationEnabled = true;
					}
					
					this.target = evt.layer;
					
					if(!evt.layer || !evt.layer.editable) {
						this.buttonMode = false;
						this.useHandCursor = false;
						this.toolTip = Catalog.getLocalizationForKey('drawing.selectDrawingLayer');
					} else {
						this.buttonMode = true;
						this.useHandCursor = true;
						this.toolTip = Catalog.getLocalizationForKey('drawing.select');
					}
				}
			}
			
			/**
			 * Update texts on map language changed
			 */
			public function onMapLanguageChange(event:I18NEvent):void{
				
				this.tooltip = Catalog.getLocalizationForKey('drawing.select');
			}
			
			// Unused functions
			public function draw():void{
				
				this.invalidateDisplayList();
			}
			public function destroy():void{}
		]]>
	</fx:Script>			
</os:IconButton>